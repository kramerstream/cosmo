use (
  alias:expected
  {mock} = ~
)

const {field, fun} = mock

export suite(__filename, proc()
  test("when well-defined, object mock must be returned", proc()
    #(1) act
    const o = mock({
      u = field.uuid()
      a = field.any("hi!")
      l1 = field.list()
      l2 = field.list([1, 2, 3])
      m1 = field.map()
      m2 = field.map({x = 1, y = 2})
      t1 = field.text("ciao!")
      t2 = field.text("bonjour!")
      x = field([{returns = 1234}, {returns = 4321}])
      y = fun({returns = 12345678})
    })

    #(2) assert
    expected(o).notToBeCallable()
    expected(o.a).toBeEqualTo("hi!")
    expected(o.u).toBeUuid()
    expected(o.l1).toBeList().toBeEmpty()
    expected(o.l2).toBeList().toBeEqualTo([1, 2, 3])
    expected(o.m1).toBeMap().toBeEmpty()
    expected(o.m2).toBeMap().toBeEqualTo({x = 1, y = 2})
    expected(o.t1).toBeText().toBeEqualTo("ciao!")
    expected(o.t2).toBeText().toBeEqualTo("bonjour!")
    expected(o.x).toBeEqualTo(1234)
    expected(o.y()).toBeEqualTo(12345678)
    expected(o.y()).toBeEqualTo(12345678)
    expected(o.u).toBeUuid()
    expected(o.x).toBeEqualTo(4321)
  end)

  test("when object mock called, JS-engine error must be raised", proc()
    #(1) act
    const o = mock({})

    #(2) assert
    expected(o).notToBeCallable()
    expected(proc() o() end).toRaise(TypeError("o is not a function"))
  end)

  test("when field() or fun() not used for member, error must be raised", proc()
    expected(proc()
      mock(x = "invalid value")
    end).toRaise(TypeError("Member 'x' must be map, field mock or function mock."))
  end)
end)
